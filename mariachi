#!/usr/local/bin/perl
use strict;
use Mail::Thread;
use File::Find::Rule;
use Date::Parse qw(str2time);
use Template;

my $maildir = shift || die "need an input maildir";
my $output = shift || die "where do we put them?";

my %names;
my @mails = map {
    $_->[0]
}
sort {
    $a->[1] <=> $b->[1]
}
map {
    open my $fh, "<$_" or die "open: '$_' $!";
    my $message = Message->new($fh);
    [ $message => str2time( $message->date ) ];
} find( file => in => $maildir );


print scalar @mails, " mails\n";

my $threader = Mail::Thread->new( @mails );
$threader->thread;

my $tt = Template->new;

$tt->process('index.tt2',
             { threads => [ $threader->rootset ] },
             "$output/index.html") or die $tt->error;

for my $mail (@mails) {
    $tt->process('message.tt2',
                 { threads => [ $threader->rootset ],
                   message => $mail,
                 },
                 "$output/".$mail->filename) or die $tt->error;
}

exit 0;

package Message;
use Mail::Internet;
use Digest::MD5 qw(md5_base64);

use base qw(Mail::Internet);

sub filename {
    my $self = shift;
    md5_base64( $self->as_string ).".html"
}

sub subject {
    my $self = shift;
    $self->head->get('subject');
}


sub date {
    my $self = shift;
    $self->head->get('date');
}

sub from {
    my $self = shift;
    my $from = $self->head->get('from');
    $from =~ s/<.*>//;
    $from =~ s/\@\S+//;
    $from;
}
